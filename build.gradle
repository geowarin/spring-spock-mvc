plugins {
  id "com.jfrog.bintray" version "1.3.1"
}

allprojects {
  repositories {
    jcenter()
  }
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'groovy'
}

group 'com.geowarin'
version '0.2'

sourceCompatibility = 1.5

repositories {
  mavenCentral()
}

dependencies {
  compile('javax.servlet:servlet-api:3.0-alpha-1') { ext.optional = true }
  compile('org.spockframework:spock-core:1.0-groovy-2.4') { ext.optional = true }
  compile('org.springframework:spring-webmvc:4.2.0.RELEASE') { ext.optional = true }
  compile('org.springframework:spring-web:4.2.1.RELEASE') { ext.optional = true }
  compile('org.springframework:spring-test:4.2.1.RELEASE') { ext.optional = true }
  compile('org.codehaus.groovy:groovy-all:2.4.4') { ext.optional = true }
  testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    JarPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom.withXml {
        def depMap = configurations.compile.dependencies.collectEntries { [it.name, it] }
        asNode().dependencies.dependency.findAll {
          def dep = depMap.get(it.artifactId.text())
          return dep?.hasProperty('optional') && dep.optional
        }.each {
          if (it.optional) {
            it.optional.value = 'true'
          } else {
            it.appendNode('optional', 'true')
          }
        }
      }
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  publications = ['JarPublication']
  pkg {
    repo = 'maven'
    name = 'spring-spock-mvc'
    desc = 'A thin layer over spring-test-mvc to make tests more spock-friendly'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/geowarin/spring-spock-mvc.git'
    version {
      name = '0.2'
      desc = 'A thin layer over spring-test-mvc to make tests more spock-friendly'
    }
  }
}
